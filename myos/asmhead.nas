; haribote-os boot asm
; TAB=4
[INSTRSET "i486p"]

VBEMODE	EQU		0x101			; 


;	0x101 :  640 x  480 x 8bit分辨率
;	0x103 :  800 x  600 x 8bit分辨率
;	0x105 : 1024 x  768 x 8bit分辨率
;	0x107 : 1280 x 1024 x 8bit分辨率

BOTPAK	EQU		0x00280000		; bootpack[h
DSKCAC	EQU		0x00100000		; fBXNLbV
DSKCAC0	EQU		0x00008000		; fBXNLbViA[hj

; BOOT_INFO信息
CYLS	EQU		0x0ff0			; u[gZN^
LEDS	EQU		0x0ff1
VMODE	EQU		0x0ff2			; FBrbgJ[H
SCRNX	EQU		0x0ff4			; xX
SCRNY	EQU		0x0ff6			; xY
VRAM	EQU		0x0ff8			; OtBbNobt@Jnn

		ORG		0xc200			; vO

; 设定画面模式,VESA BIOS extension 制作专业的BIOS，以达到不同显卡的兼容。利用它可以使用显卡的高分辨率。
;ax=4f02，bx=画面模式



;中断10号的4Fxx下的函数
　;AX=4F00 -获取VESA结构信息
　　; 01 -获取某种显示模式的属性信息
　　 ;02 -设置显示模式
　　 ;03 -获取当前的模式
　　 ;05 -页切换(用于对显示内存直接操作画点,线)
　　;其他的入口,出口参数可参看中断手册


; 确认vbe是否存在
		MOV		AX,0x9000
		MOV		ES,AX
		MOV		DI,0
		MOV		AX,0x4f00;获取显示模式
		INT		0x10
		CMP		AX,0x004f
		JNE		scrn320

; 检查VBE的版本

		MOV		AX,[ES:DI+4]
		CMP		AX,0x0200
		JB		scrn320			; if (AX < 0x0200) goto scrn320

; 获取画面模式信息

		MOV		CX,VBEMODE
		MOV		AX,0x4f01
		INT		0x10
		CMP		AX,0x004f
		JNE		scrn320

; 画面模式的确认

		CMP		BYTE [ES:DI+0x19],8 ;颜色数必须为8
		JNE		scrn320
		CMP		BYTE [ES:DI+0x1b],4 ;颜色的指定方法
		JNE		scrn320
		MOV		AX,[ES:DI+0x00] ;模式属性
		AND		AX,0x0080
		JZ		scrn320			; bit7不是就不好办

; 画面模式的切换

		MOV		BX,VBEMODE+0x4000;画面模式值+00x4000
		MOV		AX,0x4f02
		INT		0x10
		MOV		BYTE [VMODE],8	; 记下画面模式
		MOV		AX,[ES:DI+0x12]
		MOV		[SCRNX],AX
		MOV		AX,[ES:DI+0x14]
		MOV		[SCRNY],AX
		MOV		EAX,[ES:DI+0x28]
		MOV		[VRAM],EAX
		JMP		keystatus

scrn320:
		MOV		AL,0x13			; VGA图，320x200x8bit彩色
		MOV		AH,0x00
		INT		0x10
		MOV		BYTE [VMODE],8	; 记下画面模式
		MOV		WORD [SCRNX],320
		MOV		WORD [SCRNY],200
		MOV		DWORD [VRAM],0x000a0000

keystatus:
		MOV		AH,0x02
		INT		0x16 			; keyboard BIOS
		MOV		[LEDS],AL

; PICt
;	AT@dlAPICA
;	CLIOAnOAbv
;	PIC

		MOV		AL,0xff
		OUT		0x21,AL
		NOP						; OUTA@
		OUT		0xa1,AL

		CLI						; CPUx~

; CPU1MBANZXAA20GATE

		CALL	waitkbdout
		MOV		AL,0xd1
		OUT		0x64,AL
		CALL	waitkbdout
		MOV		AL,0xdf			; enable A20
		OUT		0x60,AL
		CALL	waitkbdout

; veNg[hs

[INSTRSET "i486p"]				; 486gLq

		LGDT	[GDTR0]			; bGDT
		MOV		EAX,CR0
		AND		EAX,0x7fffffff	; bit310iy[WO~j
		OR		EAX,0x00000001	; bit01iveNg[hsj
		MOV		CR0,EAX
		JMP		pipelineflush
pipelineflush:
		MOV		AX,1*8			;  \ZOg32bit
		MOV		DS,AX
		MOV		ES,AX
		MOV		FS,AX
		MOV		GS,AX
		MOV		SS,AX

; bootpack]

		MOV		ESI,bootpack	; ]
		MOV		EDI,BOTPAK		; ]
		MOV		ECX,512*1024/4
		CALL	memcpy

; fBXNf[^{u]

; u[gZN^

		MOV		ESI,0x7c00		; ]
		MOV		EDI,DSKCAC		; ]
		MOV		ECX,512/4
		CALL	memcpy

; cS

		MOV		ESI,DSKCAC0+512	; ]
		MOV		EDI,DSKCAC+512	; ]
		MOV		ECX,0
		MOV		CL,BYTE [CYLS]
		IMUL	ECX,512*18*2/4	; V_oCg/4
		SUB		ECX,512/4		; IPL
		CALL	memcpy

; asmheadSIA
;	bootpackC

; bootpackN

		MOV		EBX,BOTPAK
		MOV		ECX,[EBX+16]
		ADD		ECX,3			; ECX += 3;
		SHR		ECX,2			; ECX /= 4;
		JZ		skip			; ]
		MOV		ESI,[EBX+20]	; ]
		ADD		ESI,EBX
		MOV		EDI,[EBX+12]	; ]
		CALL	memcpy
skip:
		MOV		ESP,[EBX+12]	; X^bNl
		JMP		DWORD 2*8:0x0000001b

waitkbdout:
		IN		 AL,0x64
		AND		 AL,0x02
		JNZ		waitkbdout		; AND0waitkbdout
		RET

memcpy:
		MOV		EAX,[ESI]
		ADD		ESI,4
		MOV		[EDI],EAX
		ADD		EDI,4
		SUB		ECX,1
		JNZ		memcpy			; Z0memcpy
		RET
; memcpyAhXTCYvtBNXYAXgO

		ALIGNB	16
GDT0:
		RESB	8				; kZN^
		DW		0xffff,0x0000,0x9200,0x00cf	; \ZOg32bit
		DW		0xffff,0x0000,0x9a28,0x0047	; s\ZOg32bitibootpackpj

		DW		0
GDTR0:
		DW		8*3-1
		DD		GDT0

		ALIGNB	16
bootpack:
